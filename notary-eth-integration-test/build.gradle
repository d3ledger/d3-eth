/*
 * Copyright Soramitsu Co., Ltd. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDirs += 'src/integration-test/kotlin'
        }
        resources {
            srcDirs += ["src/integration-test/resources"]
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the integration tests.'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    setWorkingDir("$rootDir/")
    // Enable JUnit5 tests
    useJUnitPlatform()

    mustRunAfter test
}
check.dependsOn integrationTest

dependencies {
    implementation project(":eth-core")
    implementation project(":eth-bridge")

    implementation "com.github.soramitsu.soranet-validator:notary-registration:$notary_version"
    implementation "com.github.soramitsu.soranet-validator:notary-iroha-integration-test:$notary_version"
    implementation "com.github.soramitsu.soranet-validator:notifications-model:$notary_version"

    implementation "org.web3j:core:$web3j_version"
    implementation "com.squareup.moshi:moshi-kotlin:1.8.0"
    // grpc issue workaround
    api("io.grpc:grpc-netty:1.12.0") {
        force = true
    }
}
