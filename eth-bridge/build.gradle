/*
 * Copyright Soramitsu Co., Ltd. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    repositories {
        // gradle plugins repository
        gradlePluginPortal()
    }
}

plugins {
    id 'jp.co.soramitsu.sora-plugin' version '0.2.0'
}

dependencies {
    implementation project(":eth-core")
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation "com.github.soramitsu.soranet-validator:notary-commons:$notary_version"
    implementation "com.github.soramitsu.chain-adapter:chain-adapter-client:$chain_adapter_client_version"

    implementation "com.squareup.moshi:moshi-kotlin:1.8.0"
}

apply plugin: 'application'
apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = "jp.co.soramitsu.soranet.eth.bridge.EthDepositMain"
shadowDistZip.classifier = 'shadow'
shadowDistTar.classifier = 'shadow'
// name of jar file that the shadowJar plugin generates
shadowJar.archiveName = 'app.jar'

// sora-plugin configs
soramitsu {
    projectGroup = 'soranet'
    docker {
        // docker tag
        tag = System.getenv("TAG")
        // jar file that is used in the generated Dockerfile
        jar = new File("build/libs/app.jar")
        // files that will be added to the docker image (optional)
        // host fs: docker image fs
        files = ['../deploy/ethereum/contract/abi/Master.abi': '/config/abi/Master.abi']
        // the rest in configured using env variables
    }
}

task runEthDeposit(type: JavaExec) {
    main = 'jp.co.soramitsu.soranet.eth.deposit.EthDepositMain'
    classpath = sourceSets.main.runtimeClasspath
    setWorkingDir("$rootDir/")
}
